plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'org.miniProject'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.4'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('viteBuild') {
	group = 'frontend'
	description = 'Build the Vite frontend'
	doLast {
		exec {
			workingDir 'frontend'
			commandLine 'npm', 'install'
		}
		exec {
			workingDir 'frontend'
			commandLine 'npm', 'run', 'build'
		}
	}
}

tasks.register('copyFrontendToStatic') {
	dependsOn 'viteBuild'
	group = 'frontend'
	description = 'Copy built frontend files to Spring Boot static folder'
	doLast {
		copy {
			from 'frontend/dist/'
			into 'src/main/resources/static/'
		}
	}
}

bootRun {
	dependsOn 'copyFrontendToStatic'
	doFirst {
		println("Starting Spring Boot with the latest frontend build...")
	}
}